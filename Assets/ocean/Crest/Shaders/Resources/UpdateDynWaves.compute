// Crest Ocean System

// This file is subject to the MIT License as seen in the root of this folder structure (LICENSE)

// solve 2D wave equation

#pragma kernel UpdateDynWaves

#define MIN_DT 0.00001

#include "../OceanLODData.hlsl"

half _Damping;
float2 _LaplacianAxisX;
half _Gravity;
float _SimDeltaTime;
float _SimDeltaTimePrev;
float _LODChange;

// How many samples we want in one wave. trade quality for perf.
float _TexelsPerWave;

float ComputeWaveSpeed(float wavelength, float g)
{
	// wave speed of deep sea ocean waves: https://en.wikipedia.org/wiki/Wind_wave
	// https://en.wikipedia.org/wiki/Dispersion_(water_waves)#Wave_propagation_and_dispersion
	//float g = 9.81; float k = 2. * 3.141593 / wavelength; float cp = sqrt(g / k); return cp;
	const float one_over_2pi = 0.15915494;
	return sqrt(wavelength*g*one_over_2pi);
}

RWTexture2DArray<float2> _LD_TexArray_Target;

[numthreads(THREAD_GROUP_SIZE_X,THREAD_GROUP_SIZE_Y,1)]
void UpdateDynWaves(uint3 id : SV_DispatchThreadID)
{
	const float sliceIndex = id.z;
	const float sliceIndexSource = id.z + _LODChange;
	float width; float height; float depth;
	{
		_LD_TexArray_DynamicWaves_Source.GetDimensions(width, height, depth);
	}
	const float2 input_uv = IDtoUV(id.xy, width, height);
	const float2 worldPosXZ = UVToWorld(input_uv, sliceIndex);
	const float dt = _SimDeltaTime;
	const float dtp = _SimDeltaTimePrev;

	float3 uv_slice = float3(input_uv, sliceIndex);

	half2 velocity = SampleLod(_LD_TexArray_Flow, uv_slice).xy;
	float3 uv_source = WorldToUV_Source(worldPosXZ - (dt * velocity), sliceIndexSource);

	// compute axes of laplacian kernel - rotated every frame
	float e = _LD_Params_Source[sliceIndexSource].w; // assumes square RT
	float3 X = float3(_LaplacianAxisX, 0.0);
	float3 Y = float3(-X.y, X.x, 0.0);
	float fxm = SampleLod(_LD_TexArray_DynamicWaves_Source, uv_source - e*X).x; // x minus
	float fym = SampleLod(_LD_TexArray_DynamicWaves_Source, uv_source - e*Y).x; // y minus
	float fxp = SampleLod(_LD_TexArray_DynamicWaves_Source, uv_source + e*X).x; // x plus
	float fyp = SampleLod(_LD_TexArray_DynamicWaves_Source, uv_source + e*Y).x; // y plus
	half2 ft_ftm = SampleLod(_LD_TexArray_DynamicWaves_Source, uv_source).xy;
	if(sliceIndexSource > depth || sliceIndexSource < 0)
	{
		// no border wrap mode for RTs in unity it seems,
		// so make any off-array reads 0 manually
		fxm = 0;
		fym = 0;
		fxp = 0;
		fyp = 0;
		ft_ftm = half2(0, 0);
	}

	float ft = ft_ftm.x; // t - current value before update
	float ftm = ft_ftm.y; // t minus - previous value

	const float gridSize = _LD_Params[sliceIndex].x;

	// average wavelength for this scale
	float wavelength = 1.5 * _TexelsPerWave * gridSize;
	// could make velocity depend on waves
	//float h = max(waterSignedDepth + ft, 0.);
	float c = ComputeWaveSpeed(wavelength, _Gravity);

	// wave propagation
	// velocity is implicit
	float v = dtp > MIN_DT ? (ft - ftm) / dtp : 0.0;

	// damping
	v *= 1.0 - min(1.0, _Damping * dt);

	// wave equation
	float ftp = ft + dt*v + dt*dt*c*c*(fxm + fxp + fym + fyp - 4.0*ft) / (gridSize*gridSize);

	// open boundary condition, from: http://hplgit.github.io/wavebc/doc/pub/._wavebc_cyborg002.html .
	// this actually doesn't work perfectly well - there is some minor reflections of high frequencies.
	// dudt + c*dudx = 0
	// (ftp - ft)   +   c*(ft-fxm) = 0.
	if (uv_source.x + e >= 1.0) ftp = -dt*c*(ft - fxm) + ft;
	else if (uv_source.x - e <= 0.0) ftp = dt * c*(fxp - ft) + ft;
	if (uv_source.y + e >= 1.0) ftp = -dt*c*(ft - fym) + ft;
	else if (uv_source.y - e <= 0.0) ftp = dt*c*(fyp - ft) + ft;

	// attenuate waves based on ocean depth. if depth is greater than 0.5*wavelength, water is considered Deep and wave is
	// unaffected. if depth is less than this, wave velocity decreases. waves will then bunch up and grow in amplitude and
	// eventually break. i model "Deep" water, but then simply ramp down waves in non-deep water with a linear multiplier.
	// http://hyperphysics.phy-astr.gsu.edu/hbase/Waves/watwav2.html
	// http://hyperphysics.phy-astr.gsu.edu/hbase/watwav.html#c1
	float waterDepth = SampleLod(_LD_TexArray_SeaFloorDepth, uv_slice).x;
	float depthMul = 1.0 - (1.0 - saturate(2.0 * waterDepth / wavelength)) * dt * 2.0;

	ftp *= depthMul;
	ft *= depthMul;

	_LD_TexArray_Target[id] = half2(ftp, ft);
}
